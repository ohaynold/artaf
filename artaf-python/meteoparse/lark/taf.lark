start: preamble header (taf_content | TAF_NIL_CONTENT) (AMD_LIMITATION)? CLOSE
taf_content: from_group_content from_group*
TAF_NIL_CONTENT: "NIL"
AMD_LIMITATION: "AMD" /[^=]*/
CLOSE: "="

# This is rather permissive, but the preamble is the envelope, not the message
preamble: THREE_DIGITS PREAMBLE_SECOND_NUMBER preamble_issued_in preamble_issued_at? (/[A-Z]{3}/)? PREAMBLE_ISSUE_TAF
PREAMBLE_SECOND_NUMBER: /[A-Z]{4}\d{2}/
preamble_issued_in: AERODROME
preamble_issued_at: SIX_DIGITS
PREAMBLE_ISSUE_TAF: /TAF[A-Z]{3}/

header: "TAF" HEADER_AMENDMENT? header_issued_for header_issued_at "Z" header_valid_from "/" header_valid_until
header_issued_for: AERODROME
header_issued_at: SIX_DIGITS
header_valid_from: FOUR_DIGITS
header_valid_until: FOUR_DIGITS
HEADER_AMENDMENT: "AMD"
                | "COR"

from_group: "FM" SIX_DIGITS from_group_content
from_group_content: from_conditions (prob_group | tempo_group)*
prob_group: "PROB" PROB_LIKELIHOOD temporary_conditions
PROB_LIKELIHOOD: /(\d){2}/
tempo_group: "TEMPO" temporary_conditions
temporary_conditions: FOUR_DIGITS "/" FOUR_DIGITS optional_conditions

from_conditions: wind_group visibility_group phenomena_group* clouds wind_shear_group?
optional_conditions: wind_group? visibility_group? (((phenomena_group* | "NSW") clouds? wind_shear_group?)|"NSW")

wind_group: wind_direction TWO_DIGITS wind_gust_group? "KT"
wind_gust_group: "G" TWO_DIGITS
wind_shear_group: "WS" CLOUDS_ALTITUDE "/" wind_direction TWO_DIGITS "KT"
wind_direction: THREE_DIGITS
              | WIND_DIRECTION_VARIABLE
THREE_DIGITS: /\d{3}/
WIND_DIRECTION_VARIABLE: "VRB"
# Three digit wind speeds are somewhat common, but some are clearly erroneous, so we can't really
# be lenient.
TWO_DIGITS: /\d{2}/
ONE_DIGIT: /\d/

visibility_group: visibility_exceeding? visibility_range "SM"
visibility_exceeding: "P"
visibility_range: TWO_DIGITS
                | ONE_DIGIT fraction?
                | fraction
fraction: ONE_DIGIT "/" ONE_DIGIT

phenomena_group: PHENOMENON_INTENSITY? PHENOMENON+
PHENOMENON_INTENSITY: "+" | "-"
PHENOMENON: "VC"
          | "MI" | "PR" | "BC" | "DR" | "BL" | "SH" | "TS" | "FZ"
          | "DZ" | "RA" | "SN" | "SG" | "IC" | "PL" | "GR" | "GS" | "UP"
          | "BR" | "FG" | "FU" | "VA" | "DU" | "SA" | "HZ" | "PY"
          | "PO" | "SQ" | "FC" | "SS" | "DS"

clouds: clouds_sky_clear
      | clouds_vertical_visibility
      | cloud_layer+
clouds_sky_clear: "SKC"
clouds_vertical_visibility: "VV" CLOUDS_ALTITUDE cloud_layer_cumulonimbus?
cloud_layer: CLOUD_LAYER_COVERAGE CLOUDS_ALTITUDE cloud_layer_cumulonimbus?
CLOUDS_ALTITUDE: /\d{3}/
CLOUD_LAYER_COVERAGE:  "SCT" | "FEW" | "BKN" | "OVC"
cloud_layer_cumulonimbus: "CB"

SIX_DIGITS : /\d{6}/
FOUR_DIGITS: /\d{4}/
AERODROME: /[A-Z]{4}/

%import common.WS
%ignore WS
